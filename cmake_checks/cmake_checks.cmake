function(build_validate var value)
  #string(STRIP "${${var}}" __actual)
  #string(STRIP "${value}" __expected)
  string(REGEX REPLACE "^[ \t]+|[ \t]+$" "" __expected "${value}")
  string(REGEX REPLACE "^[ \t]+|[ \t]+$" "" __actual "${${var}}")
  if(NOT (" ${__actual}" STREQUAL " ${__expected}"))
    string(LENGTH "${__actual}" __actual_len)
    string(LENGTH "${__expected}" __expected_len)
    message(WARNING "Value of ${var} has been changed. Manual validation is requied.
Actual (${__actual_len}): '${__actual}'
Expected (${__expected_len}): '${__expected}'
")
  endif()
endfunction()


function(build_validate_compiler_flags var value)
  #string(STRIP "${${var}}" __actual)
  #string(STRIP "${value}" __expected)
  string(REGEX REPLACE "^[ \t]+|[ \t]+$" "" __expected "${value}")
  string(REGEX REPLACE "^[ \t]+|[ \t]+$" "" __actual "${${var}}")
  string(REGEX REPLACE " [ ]+" " " __expected "${__expected}")
  string(REGEX REPLACE " [ ]+" " " __actual "${__actual}")
  if(NOT (" ${__actual}" STREQUAL " ${__expected}"))
    string(LENGTH "${__actual}" __actual_len)
    string(LENGTH "${__expected}" __expected_len)
    message(WARNING "Value of ${var} has been changed. Manual validation is requied.
Actual (${__actual_len}): '${__actual}'
Expected (${__expected_len}): '${__expected}'
")
    if(__expected_len GREATER __actual_len)
      set(__len ${__expected_len})
    else()
      set(__len ${__actual_len})
    endif()
    foreach(__i RANGE ${__len})
      if(__expected_len GREATER __i)
        string(SUBSTRING "${__expected}" ${__i} 1 __v1)
      else()
        set(__v1 "N/A")
      endif()
      if(__actual_len GREATER __i)
        string(SUBSTRING "${__actual}" ${__i} 1 __v2)
      else()
        set(__v2 "N/A")
      endif()
      if(__v1 STREQUAL __v2)
        set(__mark "")
      else()
        set(__mark "   <== differ")
      endif()
      message(STATUS "${__i} : '${__v1}' vs '${__v2}'${__mark}")
    endforeach()
  endif()
endfunction()


function(build_validate_list var values)
  set(__has_error 0)
  set(__extra_values "")
  set(__missing_values "")
  foreach(__v ${${var}})
    set(__found 0)
    foreach(__v2 ${values})
      if(" ${__v}" STREQUAL " ${__v2}")
        set(__found 1)
        break()
      endif()
    endforeach()
    if(NOT __found)
      list(APPEND __extra_values "${__v}")
      set(__has_error 1)
    endif()
  endforeach()
  foreach(__v ${values})
    set(__found 0)
    foreach(__v2 ${${var}})
      if(" ${__v}" STREQUAL " ${__v2}")
        set(__found 1)
        break()
      endif()
    endforeach()
    if(NOT __found)
      list(APPEND __missing_values "${__v}")
      set(__has_error 1)
    endif()
  endforeach()
  if(__has_error)
    message(WARNING "Value of ${var} has been changed. Manual validation is requied.
Actual: '${${var}}'
Expected: '${values}'
  Extra entries: '${__extra_values}'
  Missing entries: '${__missing_values}'
")
  endif()
endfunction()


function(build_validate_enabled_flag var)
  if(NOT DEFINED ${var})
    message(WARNING "Manual validation is requied. Variable is not defined: ${var}")
  elseif(NOT ${var})
    message(WARNING "Manual validation is requied. Expected enabled flag: ${var}=${${var}}")
  endif()
endfunction()

function(build_validate_disabled_flag var)
  if(NOT DEFINED ${var})
    message(WARNING "Manual validation is requied. Variable is not defined: ${var}")
  elseif(${var})
    message(WARNING "Manual validation is requied. Expected disabled flag: ${var}=${${var}}")
  endif()
endfunction()

function(build_validate_disabled_or_undefined_flag var)
  if("${${var}}")
    message(WARNING "Manual validation is requied. Expected disabled flag: ${var}=${${var}}")
  endif()
endfunction()

function(build_validate_target target)
  if(NOT TARGET "${target}")
    message(WARNING "Manual validation is requied. Missing target: ${target}")
  endif()
endfunction()

function(build_validate_condition)
  if(NOT (${ARGN}))
    message(WARNING "Condition check failed: ${ARGN}")
  endif()
endfunction()
